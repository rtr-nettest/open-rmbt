buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'war'
sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

project.webAppDirName = 'WebContent'

repositories {
    mavenCentral()
    maven{url 'http://maven.restlet.com'}
    maven{url 'https://oss.sonatype.org/content/repositories/snapshots/'}
}

sourceSets {
    main {
        java { srcDir 'src' }
        resources { srcDir 'src' }
    }
}

subprojects{
    evaluationDependsOn(':RMBTSharedCode')
    evaluationDependsOn(':RMBTUtil')
}

dependencies {
    providedCompile "org.apache.tomcat:tomcat-catalina:$catalinaVersion"
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: "$servletVersion"
    compile "org.json:json:$jsonVersion"
    compile "org.restlet.jse:org.restlet.ext.json:$restletVersion"
    compile "org.restlet.jee:org.restlet.ext.servlet:$restletVersion"
    compile 'org.postgis:postgis-jdbc:1.3.3'
    compile(group: 'net.bull.javamelody', name: 'javamelody-core', version: "$javaMelodyVersion")
    compile project(':RMBTSharedCode')
    compile project(':RMBTUtil')
}


war {
    exclude 'WEB-INF/lib/**'
    exclude 'WEB-INF/classes/**'
    exclude 'META-INF/context.xml'
    from(buildDir){ 
            into('META-INF') 
            include 'context.xml' 
    }
}


def revisionPropertiesFile = file("${buildDir}/resources/main/revision.properties")
processResources {
    doLast {
        String revisionProperties = "git.describe=" + getVersionNameFromGit() + "\n"
        revisionProperties = revisionProperties + "git.branch=" + getBranchFromGit() + "\n"
        revisionProperties = revisionProperties + "git.dirty=" + getDirtyFromGit() + "\n"
        revisionProperties = revisionProperties + "git.id=" + getHashFromGit() + "\n"
        revisionProperties = revisionProperties + "git.revision=" + getVersionCodeFromGit() + "\n"
        revisionPropertiesFile.write(revisionProperties, 'UTF-8')
    }
}
